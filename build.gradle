plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'maven-publish'
}

group 'com.spotify.tracing'
version = System.getenv('TRAVIS_TAG') ?: System.getenv('DOCKER_TAG') ?: '0.0.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}


dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3"

    def opencensus_version = '0.24.0'
    implementation "io.opencensus:opencensus-api:$opencensus_version"
    implementation "io.opencensus:opencensus-impl:$opencensus_version"

    def junit_version = '5.6.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    testImplementation "io.mockk:mockk:1.9"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
        apiVersion = "1.3"
        languageVersion = "1.3"
    }
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        exporter(MavenPublication) {
            from components.java
            artifact tasks.sourceJar
        }
    }
//
//    repositories {
//        maven {
//            name = "local"
//            url = uri("$buildDir/repository")
//        }
//    }
}

